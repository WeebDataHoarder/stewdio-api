# Maintainer: minus <minus@mnus.de>

_gitname=stewdio-api
pkgname="${_gitname}-git"
_gitbranch=master
pkgver=1.0.0.r30.gb67e0b8
pkgrel=1
pkgdesc="Stewdio web radio core API server"
url="https://git.sr.ht/~minus/${_gitname}"
license=('MIT')
source=("git+https://git.sr.ht/~minus/${_gitname}#branch=${_gitbranch}")
sha512sums=('SKIP')
arch=('any')
depends=(
  'gunicorn'
  'python'
  'python-pyacoustid'
  'python-flask'
  'python-flask-sockets'
  'python-gevent'
  'python-pbkdf2'
  'python-psycopg2'
  'python-requests'
  'python-rply'
  'python-sqlalchemy'
  'python-sqlalchemy-utils'
  'python-tinytag'
)
makedepends=('git')
conflicts=("${_gitname}")
provides=("${_gitname}")
backup=('etc/stewdio/api.conf')
install=install

pkgver() {
  cd "${srcdir}/${_gitname}"
  (
    set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${srcdir}/${_gitname}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_gitname}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 contrib/archlinux/stewdio-api.service "${pkgdir}/usr/lib/systemd/system/${_gitname}.service"
  install -Dm644 stewdio-api.conf "${pkgdir}/etc/stewdio/api.conf"
}

# vim:set et sw=2 sts=2:
